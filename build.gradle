plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'newCar'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'mysql:mysql-connector-java:8.0.33'

	implementation 'com.fasterxml.jackson.core:jackson-databind'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'redis.clients:jedis:5.1.4'

	// JSON Web Token (JWT) 라이브러리
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5' // Jackson 기반 JSON 처리

}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport  // 테스트가 완료된 후 보고서를 생성하도록 설정
}

jacoco {
	toolVersion = "0.8.10"  // JaCoCo의 최신 버전 사용
}

jacocoTestReport {
	reports {
		xml.required.set(true)
		html.required.set(true)
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, excludes: [
							'**/model/**',
							'**/config/**',
							'**/*Application*',
							'**/exception/**',
							'**/controller/**',
							'**/repository/redis',
							'**/interceptor/**'
					])
				})
		)
	}
}

jacocoTestCoverageVerification {
	violationRules {//커버리지 범위와 퍼센테이지를 설정합니다
		rule {
			element = 'CLASS'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			excludes = [
					'**.*Application',
					'**.model.**',
					'**.config.**',
					'**.exception.**',
					'**.controller.**',
					'**.repository.redis',
					'**.interceptor.**'
			]
		}
	}
}

check.dependsOn jacocoTestCoverageVerification

